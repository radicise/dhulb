/*
 *
 * ioutil.dhulb
 * Created on 2023-03-25
 *
 */
#import <sys.dhulb>
#import "util.dhulb"
/%
newline:
.byte 0x0a
.byte 0x00
%/
imply u8 newline;
int readInt(int fd, *u8 next, *int dest) {
	int num = 0;
	int n = bread(fd);
	if (n < 0) {
		return n;
	}
	u8 neg = 0;
	if (n == 0x2d) {
		neg = 1;
		n = bread(fd);
		if (n < 0) {
			return n;
		}
	}
	if ((n < 0x30) | (n > 0x39)) {
		n as u8 -> next;
		0 -> dest;
		return 0;
	}
	:readInt__loop;
	num = num * 10;
	num = num + n - 0x30;
	n = bread(fd);
	if (n < 0) {
		return n;
	}
	if ((n < 0x30) | (n > 0x39)) {
		n as u8 -> next;
		if (neg) {
			(0 - num) -> dest;
			return 0;
		}
		num -> dest;
		return 0;
	}
	goto readInt__loop;
}
int readLine(int fd, int fdo, *u8 dest, uint count) {
	count = count - 1;
	uint pos = 0;
	uint mPos = 0;
	int in;
	:readLine__loop;
	in = bread(fd);
	if (in < 0) {
		return in;
	}
	/*del and mvmt*/
	if (in == 0x1b) {
		in = bread(fd);
		if (in < 0) {
			return in;
		}
		if (in == 0x5b) {
			int c;
			int er;
			er = readInt(fd, in$, c$);
			if (er < 0) {
				return er;
			}
			if ((in == 0x43) | (in == 0x44)) {
				if (in == 0x44) {
					c = 0 - c;
				}
				/*bwrite(fdo, (c + 0x23) as u8);*/
			}
			goto readLine__loop;
		}
	}
	if (in == 0x0a) {
		bwrite(fdo, in as u8);
		0 as u8 -> (dest + mPos);
		return 0;
	}
	if (mPos == count) {
		goto readLine__loop;
	}
	bwrite(fdo, in as u8);
	in as u8 -> (dest + pos);
	pos = pos + 1;
	mPos = mPos + 1;
	goto readLine__loop;
}
int print(*u8 a) {
	return write($$STDOUT_FILENO$$, a, strlen(a));
}
int print_uint(uint a) {
	int g;
	int t = 0;
	uint place = (0 - 1) as uint;
	uint ra = 1;
	:print_uint__loop2;
	place = place / 10;
	if (place != 0) {
		ra = ra * 10;
		goto print_uint__loop2;
	}
	:print_uint__loop;
	g = bwrite($$STDOUT_FILENO$$, ((a / ra) % 10) + 0x30);
	if (g < 0) {
		return g;
	}
	t = t + g;
	ra = ra / 10;
	if (ra == 0) {
		return t;
	}
	goto print_uint__loop;
}
int println(*u8 a) {
	sint g = write($$STDOUT_FILENO$$, a, strlen(a));
	if (g < 0) {
		return g;
	}
	int h = bwrite($$STDOUT_FILENO$$, 0x0a as u8);
	if (h < 0) {
		return h;
	}
	return g + h;
}
